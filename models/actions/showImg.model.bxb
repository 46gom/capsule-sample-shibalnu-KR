action (showImg) {
  description (선택한 모드에 맞춰 사진을 가져옵니다.)
  type(Search)
  
  collect {
    input (query) {
      type (ModeInfo)
      min (Required) max (One)
      
      // 유저 선택 값이 없을 경우 실행됩니다.
      default-init {
        intent{
          goal: chooseMode
        }
      }
      
      // input 값에 대하여 validate를 합니다. (https://bixbydevelopers.com/dev/docs/dev-guide/developers/modeling.modeling-actions#input-validation)
      validate{
        if (query.mode.random && query.mode.images){
          replan{
            intent{
              goal: chooseMode
            }
          }
        }
        
        if(!query.mode.random && !query.mode.images){
          halt{
            dialog{
              template-macro (chooseOneMode)
            }
          }
        }
      }
    } 
  }
  output (PhotoInfo){
   // 결과에 대한 예외처리를 합니다. 자바스크립트에서 던진 에러를 처리할 수 있습니다.
   throws{
     error (NoResult){
        on-catch {
          halt {
            dialog {
              template-macro (chooseOneMode)
            }
          }
        }
      }
      unknown-error{
        on-catch {
          halt {
            dialog {
              template-macro (unknownError)
            }
          }
        }
      }
    }
  }
}
